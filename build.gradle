plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'net.smoofyuniverse'
version = '1.2.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = "utf-8"
}

repositories {
    mavenCentral()
}

dependencies {
    api 'net.java.dev.jna:jna:5.9.0'
    testImplementation 'junit:junit:4.13.2'
}

test {
    useJUnit()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'java-keyring'
            from components.java

            pom {
                name = 'java-keyring'
                packaging = 'jar'
                description = "Java API to store passwords."
                url = 'https://github.com/Yeregorix/java-keyring'

                scm {
                    connection = 'scm:git:https://github.com/Yeregorix/java-keyring.git'
                    developerConnection = 'scm:git:https://github.com/Yeregorix/java-keyring.git'
                    url = 'https://github.com/Yeregorix/java-keyring'
                }

                licenses {
                    license {
                        name = 'Modified BSD License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id = 'Yeregorix'
                        name = 'Hugo Dupanloup'
                        email = 'yeregorix@smoofyuniverse.net'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ?
                    "https://oss.sonatype.org/content/repositories/snapshots/"
                    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username findProperty('ossrhUsername')
                password findProperty('ossrhPassword')
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign publishing.publications.mavenJava
    }
}
